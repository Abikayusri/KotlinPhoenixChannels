buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
if (project.hasProperty('signing.keyId')) {
    apply plugin: 'signing'
}

group = 'org.phoenixframework.channels'
version = '1.0.0'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.3'
    implementation 'com.squareup.okhttp3:okhttp:3.6.0'
    implementation 'org.slf4j:slf4j-api:1.7.22'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

publishing {
    publications {
        JavaPhoenixChannelsJar(MavenPublication) {
            from components.java
            groupId 'com.github.eoinsha'
            artifactId 'JavaPhoenixChannels'
            version '1.0'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'java-phoenix-channels'
        name = 'JavaPhoenixChannels'
        userOrg = 'javaphoenixchannels'
        licenses = ['BSD-3']
        vcsUrl = 'https://github.com/eoinsha/JavaPhoenixChannels.git'
    }
    publications = ['JavaPhoenixChannelsJar']
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
